{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "┌ Info: Precompiling ReachabilityAnalysis [1e97bd63-91d1-579d-8e8d-501d2b57c93f]\n",
      "└ @ Base loading.jl:1278\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "target_avoidance (generic function with 1 method)"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using ReachabilityAnalysis, SparseArrays\n",
    "\n",
    "using ReachabilityAnalysis: TaylorModelReachSet, AbstractLazyReachSet\n",
    "\n",
    "const μ = 3.986e14 * 60^2\n",
    "const r = 42164.0e3\n",
    "const r² = r^2\n",
    "const mc = 500.0\n",
    "const n² = μ / r^3\n",
    "const n = sqrt(n²)\n",
    "\n",
    "const two_n = 2*n\n",
    "const μ_r² = μ/r²\n",
    "\n",
    "# columns correspond to x, y, vx, vy, t\n",
    "const K₁ = [-28.8287 0.1005 -1449.9754 0.0046 0.0;\n",
    "            -0.087 -33.2562 0.00462 -1451.5013 0.0]\n",
    "const K₂ = [-288.0288 0.1312 -9614.9898 0.0 0.0;\n",
    "            -0.1312 -288.0 0.0 -9614.9883   0.0]\n",
    "\n",
    "const K₁mc = K₁/mc\n",
    "const K₂mc = K₂/mc\n",
    "\n",
    "function mymul!(v, A, x)\n",
    "    @inbounds for ind = 1:length(v)\n",
    "        v[ind] = zero(x[1])\n",
    "        for jind = 1:length(x)\n",
    "            v[ind] += A[ind, jind] * x[jind]\n",
    "        end\n",
    "    end\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "# dynamics in the 'approaching' mode\n",
    "@taylorize function spacecraft_approaching!(du, u, p, t)\n",
    "    x, y, vx, vy, t = u\n",
    "\n",
    "    rx = r + x\n",
    "    rx² = rx^2\n",
    "    y² = y^2\n",
    "    rc = sqrt(rx² + y²)\n",
    "    rc³ = rc^3\n",
    "    μ_rc³ = μ / rc³\n",
    "\n",
    "    uxy = Vector{typeof(x)}(undef, 2)\n",
    "    mymul!(uxy, K₁mc, u)\n",
    "\n",
    "    # x' = vx\n",
    "    du[1] = vx\n",
    "\n",
    "    # y' = vy\n",
    "    du[2] = vy\n",
    "\n",
    "    # vx' = n²x + 2n*vy + μ/(r^2) - μ/(rc^3)*(r+x) + ux/mc\n",
    "    du[3] = (n²*x + two_n*vy) + ((μ_r² - μ_rc³*rx) + uxy[1])\n",
    "\n",
    "    # vy' = n²y - 2n*vx - μ/(rc^3)y + uy/mc\n",
    "    du[4] = (n²*y - two_n*vx) - (μ_rc³*y - uxy[2])\n",
    "\n",
    "    # t' = 1\n",
    "    du[5] = one(x)\n",
    "\n",
    "    return du\n",
    "end\n",
    "\n",
    "# dynamics in the 'rendezvous attempt' mode\n",
    "@taylorize function spacecraft_attempt!(du, u, p, t)\n",
    "    x, y, vx, vy, t = u\n",
    "\n",
    "    rx = r + x\n",
    "    rx² = rx^2\n",
    "    y² = y^2\n",
    "    rc = sqrt(rx² + y²)\n",
    "    rc³ = rc^3\n",
    "    μ_rc³ = μ / rc³\n",
    "\n",
    "    uxy = Vector{typeof(x)}(undef, 2)\n",
    "    mymul!(uxy, K₂mc, u)\n",
    "\n",
    "    # x' = vx\n",
    "    du[1] = vx\n",
    "\n",
    "    # y' = vy\n",
    "    du[2] = vy\n",
    "\n",
    "    # vx' = n²x + 2n*vy + μ/(r^2) - μ/(rc^3)*(r+x) + ux/mc\n",
    "    du[3] = (n²*x + two_n*vy) + ((μ_r² - μ_rc³*rx) + uxy[1])\n",
    "\n",
    "    # vy' = n²y - 2n*vx - μ/(rc^3)y + uy/mc\n",
    "    du[4] = (n²*y - two_n*vx) - (μ_rc³*y - uxy[2])\n",
    "\n",
    "    # t' = 1\n",
    "    du[5] = one(x)\n",
    "\n",
    "    return du\n",
    "end\n",
    "\n",
    "# dynamics in the 'aborting' mode\n",
    "@taylorize function spacecraft_aborting!(du, u, p, t)\n",
    "    x, y, vx, vy, t = u\n",
    "\n",
    "    rx = r + x\n",
    "    rx² = rx^2\n",
    "    y² = y^2\n",
    "    rc = sqrt(rx² + y²)\n",
    "    rc³ = rc^3\n",
    "    μ_rc³ = μ / rc³\n",
    "\n",
    "    # x' = vx\n",
    "    du[1] = vx\n",
    "\n",
    "    # y' = vy\n",
    "    du[2] = vy\n",
    "\n",
    "    # vx' = n²x + 2n*vy + μ/(r^2) - μ/(rc^3)*(r+x)\n",
    "    du[3] = (n²*x + two_n*vy) + (μ_r² - μ_rc³*rx)\n",
    "\n",
    "    # vy' = n²y - 2n*vx - μ/(rc^3)y\n",
    "    du[4] = (n²*y - two_n*vx) - μ_rc³*y\n",
    "\n",
    "    # t' = 1\n",
    "    du[5] = one(x)\n",
    "\n",
    "    return du\n",
    "end\n",
    "\n",
    "function spacecraft(; X0 = Hyperrectangle([-900., -400., 0., 0., 0.],\n",
    "                                          [25., 25., 0., 0., 0.]),\n",
    "                         init=[(1, X0)],\n",
    "                         abort_time=(120.0, 150.0))\n",
    "\n",
    "    # variables\n",
    "    x = 1   # x position\n",
    "    y = 2   # y position\n",
    "    vx = 3  # x velocity\n",
    "    vy = 4  # y velocity\n",
    "    t = 5   # time\n",
    "    n = 4 + 1  # number of variables\n",
    "    t_abort_lower, t_abort_upper = abort_time[1], abort_time[2]\n",
    "\n",
    "    automaton = LightAutomaton(3)\n",
    "\n",
    "    # mode 1 \"approaching\"\n",
    "    invariant = HalfSpace(sparsevec([x], [1.], n), -100.) # x <= -100\n",
    "    approaching = @system(x' = spacecraft_approaching!(x), dim:5, x ∈ invariant)\n",
    "\n",
    "    # mode 2 (\"rendezvous attempt\")\n",
    "    invariant = HalfSpace(sparsevec([x], [-1.], n), 100.) # x >= -100\n",
    "    attempt = @system(x' = spacecraft_attempt!(x), dim:5, x ∈ invariant)\n",
    "\n",
    "    # mode 3 \"aborting\"\n",
    "    invariant = Universe(n)\n",
    "    aborting = @system(x' = spacecraft_aborting!(x), dim: 5, x ∈ invariant)\n",
    "\n",
    "    # transition \"approach\" -> \"attempt\"\n",
    "    add_transition!(automaton, 1, 2, 1)\n",
    "    guard = HalfSpace(sparsevec([x], [-1.], n), 100.) # x >= -100\n",
    "    t1 = @map(x -> x, dim: n, x ∈ guard)\n",
    "\n",
    "\n",
    "    # transition \"approach\" -> \"abort\"\n",
    "    add_transition!(automaton, 1, 3, 2)\n",
    "    guard_time = HPolyhedron([HalfSpace(sparsevec([t], [-1.], n), -t_abort_lower),  # t >= t_abort_lower\n",
    "                              HalfSpace(sparsevec([t], [1.], n), t_abort_upper)])   # t <= t_abort_upper\n",
    "    t2 = @map(x -> x, dim: n, x ∈ guard_time)\n",
    "\n",
    "    # transition \"attempt\" -> \"abort\"\n",
    "    add_transition!(automaton, 2, 3, 3)\n",
    "    t3 = @map(x -> x, dim: n, x ∈ guard_time)\n",
    "\n",
    "    H = HybridSystem(automaton=automaton,\n",
    "                     modes=[approaching, attempt, aborting],\n",
    "                     resetmaps=[t1, t2, t3])\n",
    "\n",
    "    return InitialValueProblem(H, init)\n",
    "end\n",
    "\n",
    "# variables\n",
    "const x, y, vx, vy, t = 1:5\n",
    "const numvars = 5\n",
    "const tan30 = tand(30)\n",
    "\n",
    "LineOfSightCone = HPolyhedron([HalfSpace(sparsevec([x], [-1.], numvars), 100.),   # x >= -100\n",
    "                    HalfSpace(sparsevec([x, y], [tan30, -1.], numvars), 0.),  # y >= x tan(30°)\n",
    "                    HalfSpace(sparsevec([x, y], [tan30, 1.], numvars), 0.),   # -y >= x tan(30°)\n",
    "                   ])\n",
    "\n",
    "Target = Hyperrectangle(zeros(2), [0.2, 0.2]);\n",
    "\n",
    "function line_of_sight(sol)\n",
    "    all_idx = findall(x -> x == 2, location.(sol))  # attempt\n",
    "    for idx in all_idx\n",
    "        verif = all(set(R) ⊆ LineOfSightCone for R in sol[idx][2:end])\n",
    "        !verif && return false\n",
    "    end\n",
    "    return true\n",
    "end\n",
    "\n",
    "function absolute_velocity(R::AbstractLazyReachSet)\n",
    "    vx = 3\n",
    "    vy = 4\n",
    "    vx2 = set(overapproximate(project(R, vars=(vx,)), Interval)).dat\n",
    "    vy2 = set(overapproximate(project(R, vars=(vy,)), Interval)).dat\n",
    "    sqrt(vx2 + vy2)\n",
    "end\n",
    "\n",
    "function absolute_velocity(R::TaylorModelReachSet)\n",
    "    Z = overapprximate(R, Zonotope)\n",
    "    absolute_velocity(Z)\n",
    "end\n",
    "\n",
    "function velocity_constraint(sol)\n",
    "    all_idx = findall(x -> x == 2, location.(sol)) # attempt\n",
    "    for idx in all_idx\n",
    "        # maximum velocity measured in m/min\n",
    "        verif = all(absolute_velocity(R) < 0.055 * 60. for R in sol[idx])\n",
    "        !verif && return false\n",
    "    end\n",
    "    return true\n",
    "end\n",
    "\n",
    "function target_avoidance(sol)\n",
    "    all_idx = findall(x -> x == 3, location.(sol)) # aborting\n",
    "    for idx in all_idx\n",
    "        verif = all(is_intersection_empty(set(Projection(R, [x, y])), Target) for R in sol[idx])\n",
    "        !verif && return false\n",
    "    end\n",
    "    return true\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "ename": "ErrorException",
     "evalue": "could not open file /home/mforets/Projects/escritoire/2020/Week26/spacecraft.jl",
     "output_type": "error",
     "traceback": [
      "could not open file /home/mforets/Projects/escritoire/2020/Week26/spacecraft.jl",
      "",
      "Stacktrace:",
      " [1] include(::String) at ./client.jl:457",
      " [2] top-level scope at In[2]:5"
     ]
    }
   ],
   "source": [
    "using BenchmarkTools, Plots, Plots.PlotMeasures, LaTeXStrings\n",
    "using BenchmarkTools: minimum, median\n",
    "\n",
    "using ReachabilityAnalysis: BoxEnclosure, BoxClustering, TemplateHullIntersection, LazyClustering\n",
    "\n",
    "include(\"spacecraft.jl\")\n",
    "\n",
    "boxdirs = BoxDirections(5)\n",
    "\n",
    "function solve_spacecraft(prob; k=25, s=missing)\n",
    "\n",
    "    # transition from mode 1 to mode 2\n",
    "    sol12 = solve(prob,\n",
    "                tspan=(0.0, 200.0),\n",
    "                alg=TMJets(abs_tol=1e-5, max_steps=10_000, orderT=5, orderQ=1, disjointness=BoxEnclosure()),\n",
    "                max_jumps=1,\n",
    "                intersect_source_invariant=false,\n",
    "                intersection_method=TemplateHullIntersection(boxdirs),\n",
    "                clustering_method=LazyClustering(1),\n",
    "                disjointness_method=BoxEnclosure())\n",
    "\n",
    "    sol12jump = overapproximate(sol12[2](120 .. 150), Zonotope)\n",
    "    t0 = tstart(sol12jump[1])\n",
    "    sol12jump_c = cluster(sol12jump, 1:length(sol12jump), BoxClustering(k, s))\n",
    "\n",
    "    # transition from mode 2 to mode 3\n",
    "    H = system(prob)\n",
    "    sol3 = solve(IVP(mode(H, 3), [set(X) for X in sol12jump_c]),\n",
    "                 tspan=(t0, 200.0),\n",
    "                 alg=TMJets(abs_tol=1e-10, orderT=7, orderQ=1, disjointness=BoxEnclosure()))\n",
    "    d = Dict{Symbol, Any}(:loc_id => 3)\n",
    "\n",
    "    return HybridFlowpipe(vcat([fp for fp in sol12.F],\n",
    "                               [Flowpipe(fp.Xk, d) for fp in sol3.F]))\n",
    "end\n",
    "\n",
    "prob = spacecraft()\n",
    "sol = solve_spacecraft(prob; k=25, s=missing)\n",
    "solz = overapproximate(sol, Zonotope)\n",
    "\n",
    "@show line_of_sight(solz)\n",
    "@show velocity_constraint(solz)\n",
    "@show target_avoidance(solz)\n",
    "\n",
    "idx_approaching = findall(x -> x == 1, location.(solz))\n",
    "idx_attempt = findall(x -> x == 2, location.(solz))\n",
    "idx_aborting = findall(x -> x == 3, location.(solz))\n",
    "\n",
    "fig = plot(legend=:bottomright)\n",
    "\n",
    "for idx in idx_approaching\n",
    "    plot!(fig, solz[idx], vars=(1, 2), lw=0.0, color=:blue, alpha=1.)\n",
    "end\n",
    "for idx in idx_attempt\n",
    "    plot!(fig, solz[idx], vars=(1, 2), lw=0.0, color=:red, alpha=1.)\n",
    "end\n",
    "for idx in idx_aborting\n",
    "    plot!(fig, solz[idx], vars=(1, 2), lw=0.0, color=:green, alpha=1.)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.0-beta1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
